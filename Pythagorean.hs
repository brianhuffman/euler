module Pythagorean where

{-
if a^2 + b^2 = c^2, and coprime a b,

then (a,b,c) must be of the form
(q^2 - p^2, 2*p*q, q^2 + p^2)  OR
(2*p*q, q^2 - p^2, q^2 + p^2)
for p < q, coprime.

k | 2*p*q
k | q^2 - p^2
k | q^2 + p^2
-------------
k | 2*q^2
k | 2*p^2

The only possible common factor of (a,b,c) is 2;
this happens iff p+q is even.
In this case, the reduced pair is generated by a smaller p', q':
triple (2i) (2j) ~~ 4 * triple i j
triple (2i+1) (2j+1) ~~ 2 * triple (j-i) (j+i+1)

--- Derivation ---
a^2 + b^2 = c^2
(a/c)^2 + (b/c)^2 = 1

Equivalent to finding rational solutions to x^2 + y^2 = 1
Positive rational solutions are 1-to-1 with rational slopes:
  m = slope of (-1,0)-(x,y) =  y / (1+x),  0 < m < 1

m = y / (1+x)
m (1+x) = y
m^2 (1+x)^2 = y^2
m^2 (1+x)^2 = 1 - x^2
m^2 (1+x)^2 = (x+1)(1-x)    (assume x <> -1)
m^2 (1+x) = (1-x)
m^2 + m^2 x = 1 - x
x + m^2 x = 1 - m^2
x (1 + m^2) = 1 - m^2
x = (1 - m^2) / (1 + m^2)

y = m (1+x)
y = m (1 + (1 - m^2) / (1 + m^2))
y = m ((1 + m^2 + 1 - m^2) / (1 + m^2))
y = 2m / (1 + m^2)
-}

pythagorean_of_ratio p q = (min a b, max a b, c)
  where (a, b, c) = (2*p*q, q^2 - p^2, q^2 + p^2)

-- all solutions to a^2 + b^2 = c^2
-- with a < b, coprime a b, a+b+c <= n
primitive_pythagorean_triples n =
  [ pythagorean_of_ratio p q |
    q <- takeWhile (\q -> 2*q^2 <= n) [2 ..],
    let ps = if even q then [1,3 .. q] else [2,4 .. q],
    p <- takeWhile (\p -> 2*q*(p+q) <= n) ps,
    gcd p q == 1 ]

pythagorean_triples m =
  [ (a*k, b*k, c*k) |
    (a,b,c) <- primitive_pythagorean_triples m,
    k <- [1 .. m `div` (a+b+c)] ]

{-
if a^2 + b^2 + ab = c^2, and coprime a b,

then (a,b,c) must be of the form
(q^2 - p^2, 2*p*q - q^2, q^2 + p^2 - p*q)  OR
(2*p*q - q^2, q^2 - p^2, q^2 + p^2 - p*q)
for p < q < 2p, coprime.

The only possible common factor of (a,b,c) is 3;
this happens iff p+q is a multiple of 3.
In this case, the reduced pair is generated by a smaller p', q':
triple (3i) (3j) ~~ 9 * triple i j
triple (3i+1) (3j+2) ~~ 3 * triple (2*j-i+1) (i+j+1)
triple (3i+2) (3j+1) ~~ 3 * triple (2*j-i) (i+j+1)

--- Derivation ---
a^2 + b^2 + ab = c^2
(a/c)^2 + (b/c)^2 + (a/c)(b/c) = 1

Equivalent to finding rational solutions to x^2 + y^2 + xy = 1
Positive rational solutions are 1-to-1 with rational slopes:
  m = -slope of (0,1)-(x,y) = (1-y) / x,  1/2 < m < 1

y = 1 - mx
x^2 + xy + y^2 = 1
x^2 + x(1 - mx) + (1 - mx)^2 = 1
x^2 + x - mx^2 + 1 - 2mx + m^2x^2 = 1
(1 - m + m^2)x^2 + (1 - 2m)x = 0
(1 - m + m^2)x + (1 - 2m) = 0        (assume x <> 0)
(1 - m + m^2)x = 2m - 1
x = (2m - 1) / (1 - m + m^2)

y = 1 - mx
y = 1 - m(2m - 1) / (1 - m + m^2)
y = 1 + (m - 2m^2) / (1 - m + m^2)
y = (1 - m + m^2 + m - 2m^2) / (1 - m + m^2)
y = (1 - m^2) / (1 - m + m^2)
-}

torricelli_of_ratio p q = (min a b, max a b, c)
  where (a, b, c) = (2*p*q - q^2, q^2 - p^2, q^2 + p^2 - p*q)

-- all solutions to a^2 + b^2 + ab = c^2
-- with a < b, coprime a b, a+b+c <= n
primitive_torricelli_triples n =
  [ torricelli_of_ratio p q |
    p <- takeWhile (\p -> 2*p^2 <= n) [2 ..],
    q <- takeWhile (\q -> q*(p+q) <= n) [p+1 .. 2*p-1],
    (p + q) `mod` 3 /= 0,
    gcd p q == 1 ]

torricelli_triples m =
  [ (a*k, b*k, c*k) |
    (a,b,c) <- primitive_torricelli_triples m,
    k <- [1 .. m `div` (a+b+c)] ]


-- See also Problem 195 (60 degree triangles)
